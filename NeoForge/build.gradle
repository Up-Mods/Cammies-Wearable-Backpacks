plugins {
    id 'idea'
    alias neoforge.plugins.neogradle
}
jarJar.enable()

dependencies {
    implementation neoforge.neoforge
    compileOnly project(":Common")

    implementation libs.resourcefulconfig.neoforge
    implementation libs.mixin.extras
    jarJar(libs.mixin.extras) {
        jarJar.ranged(it, "[${libs.versions.mixin.extras.get()},)")
    }
}

jar {
    archiveClassifier.set("slim")
}

tasks.jarJar.configure {
    archiveClassifier.set('')
}

// Automatically enable neoforge AccessTransformers if the file exists
// This location is hardcoded in FML and can not be changed.
// https://github.com/neoforged/FancyModLoader/blob/a952595eaaddd571fbc53f43847680b00894e0c1/loader/src/main/java/net/neoforged/fml/loading/moddiscovery/ModFile.java#L118
if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}

subsystems {
    parchment {
        minecraftVersion = libs.versions.minecraft.get()
        mappingsVersion = libs.versions.parchment.get()
    }
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        dependencies {
        }
    }

    client {
        modSource project.sourceSets.main
        workingDirectory project.file('run')
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

        if (project.hasProperty('mc_uuid')) {
            programArguments.add("--uuid=${project.findProperty('mc_uuid')}")
        }

        if (project.hasProperty('mc_username')) {
            programArguments.add("--username=${project.findProperty('mc_username')}")
        }

        if (project.hasProperty('mc_java_agent_path')) {
            jvmArgument("-javaagent:${project.findProperty('mc_java_agent_path')}")
        }
    }

    server {
        modSource project.sourceSets.main
        workingDirectory project.file('run/server')

        programArguments.add('nogui')
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        modSource project.sourceSets.main
        workingDirectory project.file('run')
        // this must be strings ONLY, or neogradle will not generate run configs at all!
        programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').absolutePath, '--existing', file('src/main/resources/').absolutePath)
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.named("compileJava", JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

tasks.named("javadoc", Javadoc) {
    source(project(":Common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

publishing {
    publications {
        "mavenJava${project.name}"(MavenPublication) {
            jarJar.component(it)
        }
    }
}
